plugins {
	id 'org.springframework.boot' version '2.1.9.RELEASE'
	id 'io.spring.dependency-management' version '1.0.8.RELEASE'
	id 'java'
	id 'nebula.release' version '9.2.0'
	id 'nebula.info' version '5.0.0'
	id 'com.bmuschko.docker-spring-boot-application' version '4.7.0'
}

group = 'com.jkschneider'

sourceCompatibility = '1.8'
targetCompatibility = '1.8'

repositories {
	mavenCentral()
}

dependencyManagement {
	imports {
		mavenBom "org.springframework.cloud:spring-cloud-dependencies:Greenwich.SR3"
	}
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-actuator'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.cloud:spring-cloud-starter-zipkin'
	implementation 'io.micrometer.prometheus:prometheus-rsocket-spring:0.9.0'
	
	compileOnly 'org.projectlombok:lombok:latest.release'
	annotationProcessor 'org.projectlombok:lombok:latest.release'

	testImplementation 'org.springframework.boot:spring-boot-starter-test'
}

if(hasProperty('dockerUser') && hasProperty('dockerPassword')) {
	docker {
		registryCredentials {
			username = dockerUser
			password = dockerPassword
			email = 'jkschneider@gmail.com'
		}

		springBootApplication {
			tag = "devops/canarydemo"
		}
	}
}